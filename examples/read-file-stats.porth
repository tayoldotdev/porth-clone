include "std.porth"

argc 2 < if 
    "Usage: " eputs 0 nth_argv cstr-to-str eputs " <file>\n" eputs
    "ERROR: No input file provided\n" eputs
    1 exit
end

// memory layout
memory fd sizeof(u64) end            // file descriptor memory begin
memory statbuf sizeof(stat) end    // stat structure  memory begin

// open the file
0          // mode
O_RDONLY   // flags
1 nth_argv // pathname
AT_FDCWD   // dirfd
openat

// validate file was opened successfully
dup 0 < if 
    "ERROR: could not open file `" eputs 1 nth_argv cstr-to-str eputs "` not found\n" eputs
    1 exit
end

// store file descriptor to a global variable on the memory!
fd !64

// get and validate the size of the file
statbuf fd @64 fstat 0 < if 
    "ERROR: could not determine the size of the file `" eputs 1 nth_argv cstr-to-str eputs "`\n" eputs
    1 exit
end

// print the file size stat
"Size of file `" puts
1 nth_argv cstr-to-str puts
"` is " puts
statbuf @stat.st_size putd
" bytes\n" puts
